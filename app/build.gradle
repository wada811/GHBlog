apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfiguration.getAt("compileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("buildToolsVersion")

    defaultConfig {
        applicationId globalConfiguration.getAt("applicationId")
        minSdkVersion globalConfiguration.getAt("minSdkVersion")
        targetSdkVersion globalConfiguration.getAt("targetSdkVersion")
        versionCode globalConfiguration.getAt("versionCode")
        versionName globalConfiguration.getAt("versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "CLIENT_ID", "\"${globalConfiguration.CLIENT_ID}\""
            buildConfigField "String", "CLIENT_SECRET", "\"${globalConfiguration.CLIENT_SECRET}\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CLIENT_ID", "\"${globalConfiguration.CLIENT_ID}\""
            buildConfigField "String", "CLIENT_SECRET", "\"${globalConfiguration.CLIENT_SECRET}\""
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    dataBinding {
        enabled = true
    }
}

repositories {
    maven { url 'http://github.com/wada811/Android-Material-Design-Colors/raw/master/repository/' }
}

kapt {
    generateStubs = true
}

dependencies {
    def dependencies = rootProject.ext.appDependencies
    def testDependencies = rootProject.ext.appTestDependencies
    compile dependencies.kotlin
    testCompile testDependencies.junit
    compile project(':data')
    kapt dependencies.databindingCompiler
    compile dependencies.stetho
    debugCompile dependencies.debugLeakCanary
    releaseCompile dependencies.releaseLeakCanary
    testCompile dependencies.testLeakCanary
    compile dependencies.appcompat
    compile dependencies.recyclerview
    compile dependencies.cardview
    compile dependencies.design
    compile dependencies.rxbinding
    compile dependencies.rxbindingKotlin
    compile dependencies.rxjava
    compile dependencies.rxandroid
    compile project(':RxViewModel')
    compile dependencies.threetenabp
    compile dependencies.materialDesignColors
    compile project(':LogForestAndroid')
}

task appStart(type: Exec, dependsOn: ['clean', 'installDebug']) {
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.wada811.ghblog/.view.activity.MainActivity'
}